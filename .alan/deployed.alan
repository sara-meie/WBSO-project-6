users
	dynamic: .'Users'
	user-initializer: (
		'Type' = create 'Unknown' ( )
	)

	passwords: .'Passwords'
		password-value: .'Data'.'Password'
		password-status: .'Data'.'Active'
			active: 'Yes' ( )
			reset: 'No' ( )
		password-initializer: (
			'Data' = ( )
		)


	authorities: .'Authorities'
		identities: .'Identities'>'User'
		identity-initializer: ( )

interfaces

root {


	'Users': collection ['Name']
		can-create: user .'Type'?'RVO Office'
		can-delete: user .'Type'?'RVO Office'
	{
		can-update: user .'Type'?'RVO Office'
		'Name': text
		'Type': stategroup (
			'RVO Office' { }
			'Unknown' { has-todo: user .'Type'?'RVO Office' }
			'Tax Office Employee' { }
			'Applicant' { }
		)
	}
	'Passwords': collection ['User']
		can-create: user .'Type'?'RVO Office'
		can-delete: user .'Type'?'RVO Office'
	{
		can-update: user .'Type'?'RVO Office'
		'User': text -> ^ .'Users'[]
		'Data': group {
			can-update: user is ( ^ >'User' )

			'Password': text
			'Active': stategroup (
				'No' { }
				'Yes' { }
			)
		}
	}

	'Authorities': collection ['Authority']
		can-create: user .'Type'?'RVO Office'
		can-delete: user .'Type'?'RVO Office'
	{
		can-update: user .'Type'?'RVO Office'
		'Authority': text
		'Identities': collection ['Identity'] {
			'Identity': text
			'User': text -> ^ ^ .'Users'[]
		}
	}

	'Applicant': collection ['Applicant id']
		// can-create: user .'Type'?'Applicant'
		can-delete: user .'Type'?'RVO Office'
	{
		can-read: user .'Type'?'Tax Office Employee' || user .'Type'?'RVO Office' || user .'Type'?'Applicant'
		can-update: user .'Type'?'Applicant'
		'Applicant id': text
			@default: guid
		'Type of applicant': stategroup (
			'Company with employees' { }
			'Entrepreneur with employees' { }
			'Entrepreneur without employees' { }
		)
		'KVK number': text
			@validate: "^\\d{8}$"
			@description: "please use a valid KVK number (8 digits)"
		'First of the year': stategroup (
			'Yes' { }
			'No' {
				'How many others?': number positive ' '
			}
		)
		'Projects': reference-set -> downstream ^ .'Project'* = inverse >'Applicant'
		'Project Count': number 'count' = count < 'Projects'* @hidden
		'Application Count': number ' ' = sum < 'Projects'* . 'Application Count'
	}

	'Project': collection ['Item name']
		// can-create: user .'Type'?'Applicant'
		can-delete: user .'Type'?'RVO Office'
	{ 	can-read: user .'Type'?'Tax Office Employee' || user .'Type'?'RVO Office' || user .'Type'?'Applicant'
		can-update: user .'Type'?'Applicant'
		'Item name': text
		'Applicant': text -> ^ .'Applicant'[] -<'Projects'
		'Applications': reference-set -> downstream ^ .'Application'* = inverse >'Project'
		'Application Count': number ' ' = count < 'Applications'* @hidden
	}

	'Application': collection ['app id']
		// can-create: user .'Type'?'Applicant'
		can-delete: user .'Type'?'RVO Office'
	{
		can-read: user .'Type'?'Tax Office Employee' || user .'Type'?'RVO Office' || user .'Type'?'Applicant'
		'app id': text
		// TODO should be hidden
			@default: guid
		'Project': text -> ^ . 'Project'[] -<'Applications'
		'start date': number 'date'
		'end date': number 'date'
		'employee(s)': collection ['employee id'] {
			'employee id' : text
				@default: guid
			'name': text
			'BSN': text
				@validate: "^\\d{9}$"
				@description: "please use a valid BSN number (9 digits)"
		}
		'hours worked': number 'hours'
			@description: "inschatting van het aantal S&O-uren tot en met 31 december;"
	}

}

numerical-types
	'KVK number'
	' '
	'count'
	'BSN'
	'date' in 'days' @date
	'days'
	'hours'
		@numerical-type: (
			label: "Hours per quarter"
			decimals: 2
		)
