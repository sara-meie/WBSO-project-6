users
	dynamic: .'Users'
	user-initializer: (
		'Type' = create 'Unknown' ( )
	)

	passwords: .'Passwords'
		password-value: .'Data'.'Password'
		password-status: .'Data'.'Active'
			active: 'Yes' ( )
			reset: 'No' ( )
		password-initializer: (
			'Data' = ( )
		)


	authorities: .'Authorities'
		identities: .'Identities'>'User'
		identity-initializer: ( )

interfaces

root {

	// Each user can be of three possible types,
		// - RVO office has the most amount of authority.
		// - Unkonwn is a temporary type for applicants, this type should be changed by the RVO office
		// - Tax Office Employee can only read values
		// - Applicant is a user that can apply
	'Users': collection ['Name']
		can-create: user .'Type'?'RVO Office'
		can-delete: user .'Type'?'RVO Office'
	{
		can-update: user .'Type'?'RVO Office'
		'Name': text
		'Type': stategroup (
			'RVO Office' { }
			'Unknown' { has-todo: user .'Type'?'RVO Office' }
			'Tax Office Employee' { }
			'Applicant' { }
		)
	}
	'Passwords': collection ['User']
		can-create: user .'Type'?'RVO Office'
		can-delete: user .'Type'?'RVO Office'
	{
		can-update: user .'Type'?'RVO Office'
		'User': text -> ^ .'Users'[]
		'Data': group {
			can-update: user is ( ^ >'User' )

			'Password': text
			'Active': stategroup (
				'No' { }
				'Yes' { }
			)
		}
	}

	'Authorities': collection ['Authority']
		can-create: user .'Type'?'RVO Office'
		can-delete: user .'Type'?'RVO Office'
	{
		can-update: user .'Type'?'RVO Office'
		'Authority': text
		'Identities': collection ['Identity'] {
			'Identity': text
			'User': text -> ^ ^ .'Users'[]
		}
	}

	// Applicants can be of three types, each of which needs a valid KVK number
	'Applicant': collection ['Applicant id']
		can-create: user .'Type'?'Applicant'
		can-delete: user .'Type'?'RVO Office'
	{
		can-read: user .'Type'?'Tax Office Employee' || user .'Type'?'RVO Office' || user .'Type'?'Applicant'
		can-update: user .'Type'?'Applicant'
		'Applicant id': text
			@default: guid
		'Type of applicant': stategroup (
			'Company with employees' { }
			'Entrepreneur with employees' { }
			'Entrepreneur without employees' { }
		)
		'KVK number': text
			@validate: "^\\d{8}$"
			@description: "Please use a valid KVK number (8 digits)"
		// The below lines of code enable the counting of applications per applicant
		'Projects': reference-set -> downstream ^ .'Project'* = inverse >'Applicant'
		'Project Count': number 'count' = count < 'Projects'* @hidden
		'Application Count': number ' ' = sum < 'Projects'* . 'Application Count'
	}

	'Project': collection ['Item name']
		can-create: user .'Type'?'Applicant'
		can-delete: user .'Type'?'RVO Office'
	{ 	can-read: user .'Type'?'Tax Office Employee' || user .'Type'?'RVO Office' || user .'Type'?'Applicant'
		can-update: user .'Type'?'Applicant'
		'Item name': text
		// The below lines of code enable the counting of applications per applicant
		'Applicant': text -> ^ .'Applicant'[] -<'Projects'
		'Applications': reference-set -> downstream ^ .'Application'* = inverse >'Project'
		'Application Count': number ' ' = count < 'Applications'* @hidden
	}

	// By having a separate Employee table the information entered previously can be reused.
	'Employee': collection ['Name'] {
		'Name': text
		'BSN': text
			@validate: "^\\d{9}$"
			@default: "9 digit BSN number"
	}

	// The application can be for one of the three types of applicant which results in different data validations.
	'Application': collection ['Application Id']
		can-create: user .'Type'?'Applicant'
		can-delete: user .'Type'?'RVO Office'
	{
		can-read: user .'Type'?'Tax Office Employee' || user .'Type'?'RVO Office' || user .'Type'?'Applicant'
		'Application Id': text
			@default: guid
		'Project': text -> ^ . 'Project'[] -<'Applications'
		'Start Date': number 'date'
		'End Date': number 'date'
		'Hours worked per Type of applicant': stategroup (
			'Company with employees' {
				'hours worked': number 'hours'
				@description: "Estimation of amount of R&D hours until 31st of December;"
				'How many other applications did you apply for this calendar year? ': number positive ' '
				@max: 3
				@description: "You can have a maximum of four applications per year"
				'Employee(s)': collection ['Employee'] {
					'Employee': text -> ^ ^ ^ .'Employee'[]
				}
			}
			'Entrepreneur with employees' {
				'hours worked': number 'hours'
				@description: "Estimation of amount of R&D hours until 31st of December;"
			'How many other applications did you apply for this calendar year? ': number positive ' '
			@max: 3
			@description: "You can have a maximum of 4 applications per year"
			'Employee(s)': collection ['Employee'] {
				'Employee': text -> ^ ^ ^ .'Employee'[]
			}
			}
			'Entrepreneur without employees' {
				'Hours worked': stategroup (
					'Worked less than 500 hours' {
						'hours worked': text @default: "Not eligible for WBSO application"
					}
					'Worked more than 500 hours' {
						'hours worked': number 'hours'
						@description: "Estimation of amount of R&D hours until 31st of December;"
						@min: 500
					}
				)
				'How many other applications did you apply for this calendar year? ': number positive ' '
				@max: 3
				@description: "You can have a maximum of 4 applications per year"
			}
		)
	}

}

numerical-types
	'KVK number'
	' '
	'count'
	'BSN'
	'date' in 'days' @date
	'days'
	'hours'
		@numerical-type: (
			label: "Hours per quarter"
			decimals: 2
		)
